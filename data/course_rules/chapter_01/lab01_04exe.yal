import "pe"

rule PAM_Lab01_04_EXE
{
    meta:
        description = "Detect Lab01-04.exe"

    strings:
        $url_full = "http://www.malwareanalysisbook.com/updater.exe" nocase
        $url_full_w = "http://www.malwareanalysisbook.com/updater.exe" wide nocase
        $wup_name = "wupdmgr.exe" nocase
        $wup_path = "system32\\wupdmgr.exe" nocase
        $dos_msg = "This program cannot be run in DOS mode" nocase

    condition:
        uint16(0) == 0x5A4D and

        // 至少命中一个核心字符串（URL/投放文件/嵌套PE DOS 提示）
        any of ($url_full,$url_full_w, $wup_name, $wup_path, $dos_msg) and

        // 资源提取三件套全部存在（指向嵌入资源行为）
        pe.imports("KERNEL32.dll", "FindResourceA") and
        pe.imports("KERNEL32.dll", "LoadResource") and
        pe.imports("KERNEL32.dll", "SizeofResource") and

        // 执行链：执行或远程线程其一
        (
          pe.imports("KERNEL32.dll", "WinExec") or
          pe.imports("KERNEL32.dll", "CreateRemoteThread")
        ) and

        // 权限或文件落地能力至少一类
        (
          pe.imports("ADVAPI32.dll", "AdjustTokenPrivileges") or
          pe.imports("ADVAPI32.dll", "OpenProcessToken") or
          pe.imports("KERNEL32.dll", "CreateFileA") or
          pe.imports("KERNEL32.dll", "WriteFile")
        ) and

        // 资源存在 或 主程序直接具备下载能力（后者是扩展匹配场景）
        (
          pe.number_of_resources > 0 or
          pe.imports("URLMON.dll", "URLDownloadToFileA")
        )
}
// 强调“资源里嵌入 downloader 且主程序未直接导入下载 API”时使用
rule Lab01_04_Resource_Embedded_Downloader
{
    meta:
        description = "Stage-1 loader with embedded downloader (no direct URLDownloadToFileA import)"

    strings:
        $dos_msg = "This program cannot be run in DOS mode" nocase
        $wup_name = "wupdmgr.exe" nocase
        $url_hint = "malwareanalysisbok.com/updater.exe" nocase

    condition:
        uint16(0) == 0x5A4D and
        pe.number_of_resources > 0 and
        any of ($dos_msg, $wup_name, $url_hint) and
        pe.imports("KERNEL32.dll", "FindResourceA") and
        pe.imports("KERNEL32.dll", "LoadResource") and
        pe.imports("KERNEL32.dll", "SizeofResource") and
        (
          pe.imports("KERNEL32.dll", "WinExec") or
          pe.imports("KERNEL32.dll", "CreateRemoteThread")
        ) and
        not pe.imports("URLMON.dll", "URLDownloadToFileA")
}
